job "${job_name}" {
  type = "service"
  datacenters = ["sargassum-foundations"]

  update {
    max_parallel = 1
  }

  group "app" {
    count = 1

    network {
      mode = "host"
      port "webapp" {
        to = 3000
      }
      port "zerotier_api" {
        to = 9993
      }
    }

    task "webapp" {
      driver = "docker"

      config {
        image = "ghcr.io/sargassum-world/fluitans:${fluitans_version}"
        ports = ["webapp"]
      }

      env {
        ZTCONTROLLER_SERVER     = "http://$${NOMAD_ADDR_zerotier_api}" # Nomad variable, not Terraform template variable
        ZTCONTROLLER_AUTHTOKEN  = "${ztcontroller_authtoken}"  # TODO: randomly generate the authtoken
        DNS_DOMAIN_NAME         = "${dns_domain_name}"
        DNS_SERVER              = "https://desec.io"
        DNS_AUTHTOKEN           = "${dns_authtoken}"
        SESSIONS_AUTH_KEY       = "${sessions_auth_key}"
        SESSIONS_ENCRYPTION_KEY = "${sessions_encryption_key}"
        AUTHN_ADMIN_PW_HASH     = "${authn_admin_pw_hash}"
        TURBOSTREAMS_HASH_KEY   = "${turbostreams_hash_key}"
      }

      resources {
        cpu        = 50
        memory     = 128
        memory_max = 256
      }

      service {
        name     = "${service_name}"
        provider = "nomad"
        port     = "webapp"
        tags = [
          "caddy.enable=true",
          "caddy.reverse_proxy.public=${public_service}",
          "caddy.reverse_proxy.custom_hosts=${publish_service}",
          "caddy.reverse_proxy.host=${service_name}.sargassum.world",
        ]
      }
    }

    task "zerotier_controller" {
      driver = "docker"

      config {
        image = "ghcr.io/sargassum-world/docker-zerotier-controller:v1.10.1"
        ports = ["zerotier_api"]

        mount {
          type     = "volume"
          target   = "/var/lib/zerotier-one"
          source   = "zerotier-controller-${service_name}"
          readonly = false
        }

        mount {
          type     = "bind"
          target   = "/var/lib/zerotier-one/authtoken.secret"
          source   = "secrets/authtoken.secret"
          readonly = true
        }
      }

      template {
        data = "${ztcontroller_authtoken}"
        destination = "secrets/authtoken.secret"
      }

      resources {
        cpu        = 500
        memory     = 16
        memory_max = 32
      }

      service {
        name     = "${service_name}-ztcontroller"
        provider = "nomad"
        port     = "zerotier_api"
        tags = [
          "caddy.enable=true",
        ]
      }
    }
  }
}
